
#Область ПрограммныйИнтерфейс

Процедура ПриСозданииНаСервере(Форма) Экспорт 
 
	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" 
		ИЛИ ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" 
		ИЛИ ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		
		СоздатьПолеКонтактноеЛицо(Форма.Элементы, Форма.Элементы.ГруппаШапкаПраво);
		
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда 
		
		СоздатьПолеКонтактноеЛицо(Форма.Элементы, , Форма.Элементы.Основание);
		
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда	
		
		СоздатьПолеКонтактноеЛицо(Форма.Элементы, , Форма.Элементы.СуммаДокумента);
		
	КонецЕсли; 
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда 
		
		СоздатьПолеСкидкиИКомандуПересчета(Форма);
		
	КонецЕсли;	
 
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Программное добавление элемента на форму документа
//
// Параметры:
//  Элементы - ВсеЭлементыФормы - Коллекция всех элементов формы клиентского приложения
//                 
//  Родитель - ГруппаФормы, Неопределено - Родитель для добавляемого элемента
//                
//  Элемент - ПолеФормы, Неопределено - Элемент, перед которым должен быть вставлен новый элемент
//
Процедура СоздатьПолеКонтактноеЛицо(Элементы, Родитель = Неопределено, Элемент = Неопределено)

	ПолеВвода = Элементы.Вставить("dgv_КонтактноеЛицо", Тип("ПолеФормы"), Родитель, Элемент);
	ПолеВвода.ПутьКДанным = "Объект.dgv_КонтактноеЛицо";
	ПолеВвода.Вид         = ВидПоляФормы.ПолеВвода;

КонецПроцедуры

Процедура СоздатьПолеСкидкиИКомандуПересчета(Форма)
	
	Элементы = Форма.Элементы;
	Команды  = Форма.Команды;
	
	Группа = Элементы.Добавить("dgv_ГруппаСкидкаКнопка", Тип("ГруппаФормы"), Элементы.ГруппаШапкаЛево);
	Группа.Вид                 = ВидГруппыФормы.ОбычнаяГруппа;
	Группа.Группировка         = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяЕслиВозможно;
	Группа.ОтображатьЗаголовок = Ложь;
	
	ПолеВвода = Элементы.Добавить("dgv_СогласованнаяСкидка", Тип("ПолеФормы"), Группа);
	ПолеВвода.ПутьКДанным = "Объект.dgv_СогласованнаяСкидка";
	ПолеВвода.Вид         = ВидПоляФормы.ПолеВвода;
	ПолеВвода.УстановитьДействие("ПриИзменении", "dgv_СогласованнаяСкидкаПриИзменении");
	
	Команда = Команды.Добавить("dgv_ПересчитатьТаблицу");
	Команда.Заголовок   = "Пересчитать таблицу"; 
	Команда.Действие    = "dgv_ПересчитатьТаблицу";
	Команда.Картинка    = БиблиотекаКартинок.Обновить;
	Команда.Отображение = ОтображениеКнопки.КартинкаИТекст;
	
	КнопкаФормы = Элементы.Добавить("dgv_ПересчитатьТаблицу", Тип("КнопкаФормы"), Группа);
	КнопкаФормы.ИмяКоманды = "dgv_ПересчитатьТаблицу";
	КнопкаФормы.Вид        = ВидКнопкиФормы.ОбычнаяКнопка;
	
КонецПроцедуры

#КонецОбласти
